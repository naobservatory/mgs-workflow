/***********************************************************
| WORKFLOW: DOWNSTREAM ANALYSIS OF PRIMARY WORKFLOW OUTPUT |
***********************************************************/

/***************************
| MODULES AND SUBWORKFLOWS |
***************************/

include { LOAD_DOWNSTREAM_DATA } from "../subworkflows/local/loadDownstreamData"
include { DISCOVER_RUN_OUTPUT } from "../subworkflows/local/discoverRunOutput"
include { CONCAT_RUN_OUTPUTS_BY_GROUP } from "../subworkflows/local/concatRunOutputsByGroup"
include { MARK_VIRAL_DUPLICATES } from "../subworkflows/local/markViralDuplicates"
include { VALIDATE_VIRAL_ASSIGNMENTS } from "../subworkflows/local/validateViralAssignments"
include { COUNT_READS_PER_CLADE } from "../modules/local/countReadsPerClade"
include { COPY_FILE_BARE as COPY_PYPROJECT } from "../modules/local/copyFile"
include { COPY_FILE_BARE as COPY_INPUT } from "../modules/local/copyFile"
include { SORT_TSV as SORT_ONT_HITS } from "../modules/local/sortTsv"
include { ADD_FIXED_COLUMN as PAD_ONT_COLUMNS } from "../modules/local/addFixedColumn"
include { WRITE_SENTINEL_DOWNSTREAM } from "../modules/local/writeSentinelDownstream"

/*****************
| MAIN WORKFLOWS |
*****************/

workflow DOWNSTREAM {
    main:
        // Prepare channels from input CSV file
        load_ch = LOAD_DOWNSTREAM_DATA(params.input_file, params.input_base_dir ?: projectDir)
        start_time_str = load_ch.start_time_str
        // Discover all per-sample output files and match to groups
        pipeline_pyproject_path = file("${projectDir}/pyproject.toml")
        discover_ch = DISCOVER_RUN_OUTPUT(load_ch.run_dirs, load_ch.groups, pipeline_pyproject_path, params.platform).output
        // Concatenate per-sample outputs into per-group TSVs
        concat_ch = CONCAT_RUN_OUTPUTS_BY_GROUP(discover_ch)
        // Prepare inputs for clade counting and validating taxonomic assignments
        viral_db_path = "${params.ref_dir}/results/total-virus-db-annotated.tsv.gz"
        viral_db = channel.value(viral_db_path)
        // Conditionally mark duplicates and generate clade counts based on platform
        if (params.platform == "ont") {
            // ONT: Skip duplicate marking and clade counting, but still sort by seq_id
            viral_hits_ch = SORT_ONT_HITS(concat_ch.hits, "seq_id").sorted
            // Pad with paired-end columns (NA) so ONT and short-read share the same column set
            def pad_cols = [
                "query_len_rev", "query_seq_rev", "query_qual_rev",
                "prim_align_fragment_length",
                "prim_align_best_alignment_score_rev",
                "prim_align_edit_distance_rev",
                "prim_align_ref_start_rev", "prim_align_ref_start_unclipped_rev",
                "prim_align_ref_end_unclipped_rev", "prim_align_query_rc_rev",
                "prim_align_pair_status", "prim_align_dup_exemplar",
                "sim_dup_exemplar", "sim_dup_group_size"
            ].join(",")
            viral_hits_ch = PAD_ONT_COLUMNS(viral_hits_ch, pad_cols, "NA", "padded").output
            dup_output_ch = channel.empty()
            clade_counts_ch = channel.empty()
        }
        else {
            // Short-read: mark duplicates by alignment coordinates, then by sequence
            // similarity among the reads that survive
            mark_dup_ch = MARK_VIRAL_DUPLICATES(concat_ch.hits, params.aln_dup_deviation)
            viral_hits_ch = mark_dup_ch.hits
            dup_output_ch = mark_dup_ch.stats
            // Generate clade counts
            clade_counts_ch = COUNT_READS_PER_CLADE(viral_hits_ch, viral_db).output
        }
        // Validate taxonomic assignments
        def validation_params = params.collectEntries { k, v -> [k, v] }
        validate_ch = VALIDATE_VIRAL_ASSIGNMENTS(viral_hits_ch, viral_db, params.ref_dir, validation_params)
        // Prepare publishing channels
        params_str = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(params))
        params_ch = channel.of(params_str).collectFile(name: "params-downstream.json")
        pyproject_ch = COPY_PYPROJECT(channel.fromPath(pipeline_pyproject_path), "pyproject.toml")
        input_file_ch = COPY_INPUT(channel.fromPath(params.input_file), "input_file.csv")

        // Pre-define publish-channel aggregates so we can both emit them and feed them into the sentinel barrier
        input_downstream_ch = params_ch.mix(input_file_ch)
        logging_downstream_ch = pyproject_ch
        results_downstream_ch = dup_output_ch.mix(
                                    clade_counts_ch,
                                    validate_ch.annotated_hits,
                                    concat_ch.other,
                                    concat_ch.fastp_json)

        // Validate published outputs and write per-group sentinels
        groups_only_ch = load_ch.groups
            .map { _label, _sample, group -> group }
            .unique()
        sentinel_params = params + [output_dir: "${params.base_dir}/output", pyproject_path: "${projectDir}/pyproject.toml"]
        sentinel_ch = WRITE_SENTINEL_DOWNSTREAM(
            groups_only_ch,
            input_downstream_ch.mix(logging_downstream_ch, results_downstream_ch).collect(),
            start_time_str,
            sentinel_params
        )

    emit:
        input_downstream = input_downstream_ch
        logging_downstream = logging_downstream_ch
        intermediates_downstream = validate_ch.blast_results
        results_downstream = results_downstream_ch
        experimental_downstream = channel.empty()
        sentinel_downstream = sentinel_ch.sentinel
}
