nextflow_process {

    name "Test Process COUNT_READS"
    script "modules/local/countReads/main.nf"
    process "COUNT_READS"

    test("Should run without failures") {
        config "tests/run.config"
        setup {
            run("LOAD_SAMPLESHEET") {
                script "subworkflows/local/loadSampleSheet/main.nf"
                process {
                    """
                    input[0] = "${projectDir}/test-data/samplesheet.csv"
                    input[1] = false
                    input[2] = false
                    """
                }
            }
        }

        when {
            params {}
            process {
                """
                input[0] = LOAD_SAMPLESHEET.out.samplesheet
                """
            }
        }

        then {
            // Should run without failures
            assert process.success
            // Should have expected structure
            def file_out = path(process.out.output[0]).csv(sep: "\t") 
            assert file_out.rowCount == 1
            assert file_out.columnNames == ["sample", "total_read_count"]
            // Read count should be correct
            def sample_name = process.out.input[0][0]
            def fastq_in = path(process.out.input[0][1][0]).fastq
            def reads_in = fastq_in.getNumberOfRecords()
            assert file_out.rows[0] == ["total_read_count": reads_in, "sample": sample_name]
        }

    }

}
