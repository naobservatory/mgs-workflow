def countGzipLines = { file -> path(file).linesGzip.size() }
def getGzipHeader = { file -> ["bash", "-c", "zcat " + file + " | head -n 1"].execute().text.trim() }
def getGzipBody = { file -> ["bash", "-c", "zcat " + file + " | tail -n +2"].execute().text.trim() }

nextflow_process {

    name "Test process REHEAD_TSV"
    script "modules/local/reheadTsv/main.nf"
    process "REHEAD_TSV"
    config "tests/run.config"

    test("Should fail with an appropriate error code if the field to modify is not present."){
        when {
            params {}
            process {
                '''
                input[0] = Channel.of("test")
                    | combine(Channel.of("${projectDir}/test-data/toy-data/test_tab_sorted.tsv.gz"))
                input[1] = "xyz" // Not in header
                input[2] = "abc"
                input[3] = "test"
                '''
            }
        }
        then {
            assert process.failed
            assert process.exitStatus == 1
            assert process.errorReport.contains("ERROR: Target field not found in header")
        }
    }

    test("Should produce no change when old and new headers match"){
        when {
            params {}
            process {
                '''
                input[0] = Channel.of("test")
                    | combine(Channel.of("${projectDir}/test-data/toy-data/test_tab_sorted.tsv.gz"))
                input[1] = "x"
                input[2] = "x"
                input[3] = "test"
                '''
            }
        }
        then {
            // Should run without failures
            assert process.success
            // Output should match input
            assert path(process.out.output[0][1]).md5 == path(process.out.input[0][1]).md5
        }
    }

    test("Should successfully change the header when old and new fields differ"){
        when {
            params {}
            process {
                '''
                input[0] = Channel.of("test")
                    | combine(Channel.of("${projectDir}/test-data/toy-data/test_tab_sorted.tsv.gz"))
                input[1] = "x"
                input[2] = "test"
                input[3] = "test"
                '''
            }
        }
        then {
            // Should run without failures
            assert process.success
            // Output and input dimensions should match
            def tab_in = path(process.out.input[0][1]).csv(sep: "\t", decompress: true)
            def tab_out = path(process.out.output[0][1]).csv(sep: "\t", decompress: true)
            assert tab_out.columnCount == tab_in.columnCount
            assert tab_out.rowCount == tab_in.rowCount
            // Output and input headers should match except for intended substitution
            assert tab_out.columnNames != tab_in.columnNames
            assert tab_out.columnNames == tab_in.columnNames*.replace("x", "test")
            // Rest of file should be unmodified
            for (c in tab_in.columnNames) {
                if (c == "x"){
                    assert tab_in.columns[c] == tab_out.columns["test"]
                } else {
                    assert tab_in.columns[c] == tab_out.columns[c]
                }
            }
        }
    }

}
