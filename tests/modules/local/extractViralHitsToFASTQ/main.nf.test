def countGzipLines = { file -> path(file).linesGzip.size() }
nextflow_process {

    name "Test process EXTRACT_VIRAL_HITS_TO_FASTQ"
    script "modules/local/extractViralHitsToFASTQ/main.nf"
    process "EXTRACT_VIRAL_HITS_TO_FASTQ"
    config "tests/run.config"


    test("Should fail with appropriate error when seq_id is missing from input tsv") {
        tag "expect_failed"
        when {
            params {}
            process {
                '''
                input[0] = "${projectDir}/test-data/toy-data/test_tab_sorted.tsv.gz"
                input[1] = "${projectDir}/test-data/toy-data/test-random.fastq.gz"
                '''
            }
        }
        then {
            assert process.failed
            assert process.exitStatus == 1
            assert process.errorReport.contains("ERROR: No column named 'seq_id' in header")
        }
    }

    test("On paired data, should run without errors and preserve lines") {
        tag "expect_success"
        when {
            params {}
            process {
                '''
                input[0] = "${projectDir}/test-data/toy-data/virus-hits-test.tsv.gz"
                input[1] = "${projectDir}/test-data/toy-data/test-random.fastq.gz"
                '''
            }
        }
        then {
            // Should run without failures
            assert process.success
            // Output IDs should match input TSV
            def tab_in = path(process.out.input_tsv[0]).csv(sep: "\t", decompress: true)
            def ids_out = path(process.out.ids[0]).csv(sep: "\t", decompress: true, header: false)
            assert ids_out.columnCount == 1
            assert tab_in.rowCount == ids_out.rowCount
            assert tab_in.columns["seq_id"] == ids_out.columns["C0"]
            // Output FASTQ should have appropriate line count (interleaved)
            def fastq_in = path(process.out.input_fastq[0]).fastq
            def fastq_out = path(process.out.fastq[0]).fastq
            assert fastq_in.getNumberOfRecords() % 2 == 0
            assert fastq_out.getNumberOfRecords() % 2 == 0
            // Output FASTQ should be subset of input FASTQ
            assert fastq_out.getNumberOfRecords() < fastq_in.getNumberOfRecords()
            for (r in fastq_out.readNames) {
                assert r in fastq_in.readNames
            }
            // Output FASTQ should only have IDs from input TSV
            def fastq_ids_in = fastq_in.readNames.collect{ it.tokenize(" ")[0] }
            def fastq_ids_out = fastq_out.readNames.collect{ it.tokenize(" ")[0] }
            for (id in fastq_ids_in) {
                if (id in ids_out.columns["C0"]) {
                    assert id in fastq_ids_out
                } else {
                    assert !(id in fastq_ids_out)
                }
            }
        }
    }
}
